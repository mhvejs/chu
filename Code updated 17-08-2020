//Problems to be fixed:
//Rotate player character on jump
//Enemy box rotate alongside with player movement to always face "front"

// ---------------------------------------------------------------------------------------- INITIAL

import java.awt.*;

Robot robot;

float cameraRotateX;
float cameraRotateY;
float cameraSpeed;

float angle;

boolean wPressed, sPressed, aPressed, dPressed;
PVector pressedDir = new PVector();
float accelMag;

Player player; 
Enemy enemy;
Hit hit;
Bullet bullet;

com.jogamp.newt.opengl.GLWindow myGLWindow;

float gridLevelY; 

PImage img;
// ---------------------------------------------------------------------------------------- SETUP

void setup() {
  fullScreen(P3D);
  smooth(8);
  img = loadImage("black.jpg");
  myGLWindow=getFrame(getSurface());

  accelMag = 2;
  player = new Player();
  enemy = new Enemy();
  hit = new Hit();
  bullet = new Bullet();

  gridLevelY = height/2; 

  cameraSpeed = TWO_PI / width;
  cameraRotateY = -PI/6;

  try {
    robot = new Robot();
  } 
  catch(Exception ex) {
    println(ex);
  }

  noCursor();
}

// ---------------------------------------------------------------------------------------- DRAW

void draw() {
  background(125);
  lights();

  updateRotation();

  translate(width/2, height/10);
  rotateX(cameraRotateY);
  rotateY(cameraRotateX);
  translate(-player.pos.x, 0, -player.pos.z);

  player.display(); 
  player.move();

  bullet.playerdisplay();
  bullet.playermovement();

  hit.display();

  drawGrid();

  enemy.display();
  enemy.move();

  angle += mouseChangeX()*0.003;
  robot.mouseMove(getSketchCenterX(), getSketchCenterY());
}

// ---------------------------------------------------------------------------------------- GRID

void drawGrid() {
  pushMatrix();
  int count = 50;
  translate(0, gridLevelY);
  stroke(255);
  float size = (count -1) * player.size*2;
  for (int i = 0; i < count; i++) {
    float pos2 = map(i, 0, count-1, -0.5 * size, 0.5 * size);
    line(pos2, 0, -size/2, pos2, 0, size/2);
    line(-size/2, 0, pos2, size/2, 0, pos2);
  }
  popMatrix();
}

// ---------------------------------------------------------------------------------------- CAMERA

int getSketchCenterX() {
  return
    myGLWindow.getX() + width / 2;
}

int getSketchCenterY() {
  return 
    myGLWindow.getY() + height / 2;
}

static final com.jogamp.newt.opengl.GLWindow getFrame(final PSurface surface) {
  // used only once ! 
  return 
    (com.jogamp.newt.opengl.GLWindow) surface.getNative();
}

float mouseChangeX() {
  return 
    getSketchCenterX() - (float) MouseInfo.getPointerInfo().getLocation().getX();
}

float mouseChangeY() {
  return
    getSketchCenterY() - (float) MouseInfo.getPointerInfo().getLocation().getY();
}

void updateRotation() {
  cameraRotateX -= mouseChangeX() * cameraSpeed;
  cameraRotateY += mouseChangeY() * cameraSpeed;
  cameraRotateY = constrain(cameraRotateY, -HALF_PI, 0);
}

PVector getMovementDir() {
  return pressedDir.copy().normalize();
}

// ---------------------------------------------------------------------------------------- PLAYER MOVEMENT

void mouseClicked() {
  //only used so I can keep track of coordinates in case I need to change things
  println("X " + mouseX);
  println("Y " + mouseY);
  println("EnemyX " + enemy.pos.x);
  println("PlayerX " + player.pos.x);
}

void keyPressed() {
  if (keyCode == SHIFT) { //dash
    accelMag = 4;
  }

  switch(keyCode) {

  case 32:
    player.startJump(); 
    break; 

  case 87:
    wPressed = true;
    pressedDir.y = -1;
    break;

  case 65:
    aPressed = true;
    pressedDir.x = -1;
    break;

  case 83:
    sPressed = true;
    pressedDir.y = 1;
    break;

  case 68:
    dPressed = true;
    pressedDir.x = 1;
    break;
  }
}

void keyReleased() {
  if (keyCode == SHIFT) { //dash end
    accelMag = 2;
  }

  switch(keyCode) {

  case 87:
    wPressed = false;
    pressedDir.y = sPressed ? 1 : 0;
    break;

  case 65:
    aPressed = false;
    pressedDir.x = dPressed ? 1 : 0;
    break;

  case 83:
    sPressed = false;
    pressedDir.y = wPressed ? -1 : 0;
    break;

  case 68:
    dPressed = false;
    pressedDir.x = aPressed ? -1 : 0;
    break;
  }
}

// ---------------------------------------------------------------------------------------- PLAYER CLASS + JUMP

class Player {
  float size  = 100;
  boolean jump = false;
  int bounceCount;
  float gravity = 1.5;

  PVector pos, speed;

  Player() {
    pos = new PVector(0, height/2 + size/2, 0);
    speed = new PVector();
  }

  void display() {
    pushMatrix();
    translate(pos.x, pos.y, pos.z);
    stroke(255);
    fill(0);
    rotateY(atan2(speed.x, speed.z));
    box(size);
    popMatrix();
  }

  void move() {
    PVector accel = getMovementDir().rotate(cameraRotateX).mult(accelMag);
    accel.set(accel.x, gravity, accel.y);
    speed.add(accel);
    pos.add(speed);
    speed.mult(0.9);
    jumpManagement();
  }

  void startJump() {
    if (!jump) {
      jump = true;
      bounceCount = 0;
      speed.y = -35; //height of jump
    }
  }

  void jumpManagement() {
    if (pos.y >= height/2 - size/2) {
      bounceCount++;
      pos.y = height/2 - size/2;
      speed.y *= -0.8;
      jump = false;
      if (bounceCount > 5) {
        speed.y = 0;
      }
    }
  }
}

// ---------------------------------------------------------------------------------------- ENEMY CLASS #1

class Enemy {
  float size = 100;
  PVector pos;
  float speed = 15;

  Enemy() {
    pos = new PVector(random(width), height/2 - size/2, random(0, 301));
  }

  void display() {
    pushMatrix();
    translate(pos.x, pos.y, pos.z);
    fill(255, 0, 0);
    box(size);
    popMatrix();
  }

  void move() {
    PVector dir = PVector.sub(player.pos, pos);
    dir.y = 0;
    dir.setMag(speed);
    pos.add(dir);
  }
}

// ---------------------------------------------------------------------------------------- PLAYER BULLET CREATION

class Bullet {
  float size = 15;
  
  Bullet() {

  }

  void playerdisplay() {
    pushMatrix();
    translate(player.pos.x + player.size + 50, player.pos.y, player.pos.z);
    sphere(size);
    popMatrix();
  }
  void playermovement() {
    
  }
}

// ---------------------------------------------------------------------------------------- COLLISION/DEATH/END-SCREEN CLASS

class Hit {

  Hit() {
    
  }

  void display() {
    if (player.pos.dist(enemy.pos) < player.size) {
      background(0);
      image(img, 0, 0, 1080, 1920);
    }
  }
}
